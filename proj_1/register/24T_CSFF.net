* 24T change-sensing FF (CSFF)
.subckt csff d q clk vdd vss
* master 
Xmaster_t1    vdd       d    master_1    vdd  pfet                  
Xmaster_t2    master_1  clk  DN          vdd  pfet                     
Xmaster_t3    DN        CS   csu_2       vss  nfet                     
Xmaster_t10   vdd       DI   master_2    vdd  pfet                     
Xmaster_t11   master_2  CS   DN          vdd  pfet                     
Xmaster_t12   DN        DI   csu_2       vss  nfet                     
Xmaster_i1    DN  DI  vdd  vss  inv_size_1
* change-sensing unit 
Xcsu_t4    vdd       clk  CS          vdd  pfet                     
Xcsu_t5    CS        clk  csu_1       vss  nfet                     
Xcsu_t6    csu_1     QN   csu_2       vss  nfet                     
Xcsu_t7    csu_2     d    vss         vss  nfet                     
Xcsu_t8    csu_1     DN   csu_3       vss  nfet                     
Xcsu_t9    csu_3     QI   vss         vss  nfet                     
* slave
Xslave_t15    master_2 CS    QN        vdd  pfet                    
Xslave_t16    QN       clk   slave_1   vss  nfet                    
Xslave_t17    slave_1  DI    vss       vss  nfet                    
Xslave_i2     QN  q    vdd   vss  inv_size_1
Xslave_i3     QN  QI   vdd   vss  inv_size_1
Xslave_t22    vdd      QI    slave_2   vdd  pfet                    
Xslave_t23    slave_2  clk   QN        vdd  pfet                    
Xslave_t24    QN       CS    csu_3     vdd  pfet                    
.ends csff